/*==================================================
project:       graph pcn compare results
Author:        David L. Vargas
E-email:       dvargasm@worldbank.org
url:           
Dependencies:  The World Bank
----------------------------------------------------
Creation Date:    15 Jun 2020 - 12:41:59
Modification Date:   
Do-file version:    01
References:          
Output:             
==================================================*/

/*==================================================
              0: Program set up
==================================================*/
program define pcn_compare_rp, rclass
syntax  [anything(name=subcmd id="subcommand")], ///
[                                              ///
server(string)                                  ///
VARiables(string)                               ///
DIRSave(string)								  ///
SDLevel(string)								  ///
TOLerance(integer 3)                           /// decimal places
]

version 15


//========================================================
// Start
//========================================================

qui{
	/*================================================
	1: Check options definition and declare macros
	==================================================*/
	// Check for sepscatter packg
	cap which sepscatter
	if (_rc) noi ssc install sepscatter
	
	cap which confirmdir
	if (_rc) noi ssc install confirmdir
	
	// relevant macros 
	if ("`variables'"=="") loc variables = "headcount"
	else                  loc variables = lower("`variables'")
	
	if ("`server'" == "") loc server "AR"
	else                  loc server = lower("`server'")
	
	if ("`sdlevel'" == "") loc sdlevel = 2
	
	cap loc sdlevel = real("`sdlevel'")
	if (_rc){
		noi di as err "The SD level must be a real number" char(10)
		noi di "SD level set to default"
	}
	
	if ("`dirsave'" != "") {
		if (lower("`dirsave'") == "wd")   loc dirsave "`c(pwd)'"
		else                       confirmdir "`dirsave'"
	}
	else{
	noi dis as err "No saving directory specified. STOP" char(10) as text "Either provide a directory or set dirsave(wd) to use working directory."
	err
	}
	
	/*================================================
	2: Get comparison data and graphs
	==================================================*/

	pcn_compare, mainv(`variables') server(`server') check("main")
	pcn_compare_gr, variables(`variables') dirsave(`dirsave') ///
	   sdlevel(`sdlevel') tolerance(`tolerance')

	/*================================================
	3: Report 
	==================================================*/
	putdocx begin

	// Add a title
	putdocx paragraph, style(Title) 
	putdocx text ("Report: Current vs Testing Povcalnet")
	
	putdocx paragraph
	putdocx text ("This report compares the currently publish povcalnet to the unpublish version on the testing server. This file was generated by the PCN package, with the following user given conditions:"), linebreak(1)
	putdocx text ("- variables: `variables'"), linebreak(1)
	putdocx text ("- server: `server'"), linebreak(1)
	putdocx text ("- SD level: `sdlevel'"), linebreak(1)
	putdocx text ("- Decimal points tolerance: `tolerance'"), linebreak(1)
	
	// Overview
	putdocx paragraph, style(Heading1)
	putdocx text ("Overview of changes")
	
	putdocx paragraph
	putdocx text ("The year-country points, have the following status:"), linebreak(1)
	
	preserve
	contract status	
	rename _freq occurrences
	putdocx table mytable = data(status occurrences), varnames width(50%)
	restore
	
	putdocx sectionbreak 
	
	// by variable
	
	levelsof regioncode, local(regions)
	
	putdocx paragraph
	putdocx text ("Analysing by variable we have the following"), linebreak(1)
	
	foreach var of local variables{
		putdocx paragraph, style(Heading1)
		putdocx text ("Changes on `var'")
		
		putdocx paragraph
		putdocx text ("Regarding the `var':"), linebreak(2)
		putdocx text ("The differences across servers have the following stats:"), linebreak(1)
		
		loc vars "d_`var'"
		forv x = 1/`sdlevel' {		
			loc vars "`vars' ht_`x'sd_`var'"

		}
		local nvars: word count `vars'
		local nrows=`nvars'+2 
		
		putdocx table table2 = (`nrows',7), border(all, nil) width(100%) layout(autofitcontents) note(Note: The differences are computed as current - testing values.)
		putdocx table table2(1,1)=("Summary Statistics"), bold font("Calibri", 12) halign(center) colspan(7) linebreak
		putdocx table table2(2,1) = ("Variable"), bold font("Calibri", 11) halign(center)
		putdocx table table2(2,2) = ("Variable Definition"), bold font("Calibri", 11) halign(center)
		putdocx table table2(2,3) = ("Obs."), bold font("Calibri", 11) halign(center)
		putdocx table table2(2,4) = ("Mean"), bold font("Calibri", 11) halign(center)
		putdocx table table2(2,5) = ("Std. Dev."), bold font("Calibri", 11) halign(center)
		putdocx table table2(2,6) = ("Min."), bold font("Calibri", 11) halign(center)
		putdocx table table2(2,7) = ("Max."), bold font("Calibri", 11) halign(center)
		
		local i=3
		foreach vh of local vars {
			local lab: variable label `vh'
			quietly summarize `vh'
			putdocx table table2(`i',1) = ("`vh'")
			putdocx table table2(`i',2) = ("`lab'")
			putdocx table table2(`i',3) = (r(N))
			putdocx table table2(`i',4) = (r(mean)), nformat(%5.2f)
			putdocx table table2(`i',5) = (r(sd)), nformat(%5.2f)
			putdocx table table2(`i',6) = (r(min))
			putdocx table table2(`i',7) = (r(max))
			local i=`i'+1
		}
		
		putdocx paragraph
		putdocx text ("The differences beyond the `sdlevel' SD from the mean are distributed as follows:"), linebreak(1)
		
		putdocx image hist_d_`var'.png
		
		putdocx paragraph
		putdocx text ("Differencing by region:"), linebreak(1)
		
		putdocx image histR_d_`var'.png
		
		// freq table by region
		
		putdocx paragraph
		putdocx text ("By region and level of SD from the mean we have:"), linebreak(1)
		
		loc vars ""
		forv x = 1/`sdlevel' {		
			loc vars "`vars' ht_`x'sd_`var'"

		}
		local nreg: word count `regions'
		local nrows=`nreg'+2
		
		loc j = 3
		foreach vh of local vars {
			local lab: variable label `vh'
		
			putdocx table table`j' = (`nrows',4), border(all, nil) width(100%) layout(autofitcontents) note("Note: The variable is a dummy, with value of one if the absolute difference is higher than the mean plus the selected number of standard deviations.")
			putdocx table table`j'(1,1)=("`lab' by region"), bold font("Calibri", 12) halign(center) colspan(7) linebreak
			putdocx table table`j'(2,1) = ("Region"), bold font("Calibri", 11) halign(center)
			putdocx table table`j'(2,2) = ("1"),  bold font("Calibri", 11) halign(center)
			putdocx table table`j'(2,3) = ("0"),  bold font("Calibri", 11) halign(center)
			putdocx table table`j'(2,4) = ("Total"),  bold font("Calibri", 11) halign(center)

			local i=3
			foreach rg of local regions {
				qui count if regioncode == "`rg'" & `vh' == 1
				loc uno = r(N)
				qui count if regioncode == "`rg'" & `vh' == 0
				loc zero = r(N)
				qui count if regioncode == "`rg'"
				loc tot = r(N)
				
				putdocx table table`j'(`i',1) = ("`rg'")
				putdocx table table`j'(`i',2) = ("`uno'")
				putdocx table table`j'(`i',3) = ("`zero'")
				putdocx table table`j'(`i',4) = ("`tot'")
				local i=`i'+1
			}
			loc j = `j' + 1
		}
		
		// Difference scatter
		
		putdocx paragraph
		putdocx text ("Scatter plot of differences:"), linebreak(1)
		
		putdocx image sc_`var'.png
		
		putdocx paragraph
		putdocx text ("The following countries have differences in headcount beyond the tolerance level of `SDLevel' deviation of the mean:"), linebreak(1)
		
		// problematic observations by region table by region		
		
		tempvar obsid
		*countrycode year povertyline coveragetype datatype
		*egen `obsid' = concat(countrycode year coveragetype datatype), p(-)
		egen `obsid' = concat(countrycode year), p(-)
		loc vars ""
		forv x = 1/`sdlevel' {		
			loc vars "`vars' ht_`x'sd_`var'"
		}
		local nreg: word count `regions'
		local nrows=`nreg'+2
		
		foreach vh of local vars {
			local lab: variable label `vh'
			putdocx table table`j' = (`nrows',2), border(all, nil) width(100%) layout(autofitcontents) note("Note: The variable is a dummy, with value of one if the absolute difference is higher than the mean plus the selected number of standard deviations.")
			putdocx table table`j'(1,1)=("countries `lab' by region'"), bold font("Calibri", 12) halign(center) colspan(7) linebreak
			putdocx table table`j'(2,1) = ("observation"), bold font("Calibri", 11) halign(center)

			local i=3
			foreach rg of local regions {
				qui levelsof `obsid' if regioncode == "`rg'" & `vh' == 1, clean s(", ") local(obsi)
				putdocx table table`j'(`i',1) = ("`rg'")
				putdocx table table`j'(`i',2) = ("`obsi'")
				local i=`i'+1
			}
			loc j = `j' + 1
		}
		
		
		
		putdocx sectionbreak 	
	}
	
	
	putdocx save "`dirsave'/pcn_report.docx", replace
	noi di as result "Graphs and report (pcn_report.docx) saved at `dirsave'."

}

end
exit
/* End of do-file */

><><><><><><><><><><><><><><><><><><><><><><><><><><><><><><><><><><><><><><><><

Notes:
1.
2.
3.


Version Control:

cd "~\OneDrive - WBG\Desktop"
pcn_compare_rp, dirs(wd) var(headcount)
